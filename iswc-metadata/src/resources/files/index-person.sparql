PREFIX foaf:   <http://xmlns.com/foaf/0.1/>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX swrc:   <http://swrc.ontoware.org/ontology#>
PREFIX swc:    <http://data.semanticweb.org/ns/swc/ontology#>

SELECT  ?name ?uri
        ?conf_uri
		(GROUP_CONCAT(distinct ?role_label ; SEPARATOR="; ") as ?roles)
		(count(distinct ?role_label) AS ?cnt_role)
		(count(distinct ?uri_paper ) AS ?cnt_paper)
		(GROUP_CONCAT(distinct ?homepage_x ; SEPARATOR="; ") as ?homepage)
		?bio  ?depiction
		(GROUP_CONCAT(distinct ?mbox_sha1sum_x ; SEPARATOR="; ") as ?mbox_sha1sum)
		(count(distinct ?mbox_sha1sum_x) AS ?cnt_mbox)
		(SAMPLE(?affiliationLabel) as ?organization_sample)
		(GROUP_CONCAT(distinct ?affiliationLabel ; SEPARATOR="; ") as ?organization)
		(GROUP_CONCAT(distinct ?affiliation ; SEPARATOR="; ") as ?organization_uri)
		(count(distinct ?affiliation) AS ?cnt_org)
WHERE {
	?uri rdfs:label ?name .
	?uri a foaf:Person.
	?conf_uri a swc:ConferenceEvent .

	OPTIONAL{
		?role_event rdfs:label ?role_event_label.
		?role_event a ?role_event_type.
		?role rdfs:label ?role_label .
		?role a ?role_type.
		?role swc:isRoleAt ?role_event .
		?role swc:heldBy ?uri.
	}
		
	OPTIONAL{
		?uri foaf:made ?uri_paper .
		?uri_paper a swrc:InProceedings .
	}	

	OPTIONAL{
		?uri swrc:affiliation ?affiliation .
		?affiliation rdfs:label ?affiliationLabel .
	}	
	OPTIONAL{
		?uri swrc:bio ?bio .
	}
	OPTIONAL{
		?uri foaf:homepage ?homepage_x .
	}
	OPTIONAL{
		?uri foaf:mbox_sha1sum ?mbox_sha1sum_x .
	}
	OPTIONAL{
		?uri foaf:depiction ?depiction .
	}
}
GROUP BY 
		?conf_uri
		?name ?uri
		?bio ?depiction
ORDER BY 
		?uri 
